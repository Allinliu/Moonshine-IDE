<?xml version="1.0" encoding="utf-8"?>
<resizabletitlewindow:ResizableTitleWindow
	xmlns:fx="http://ns.adobe.com/mxml/2009"
	xmlns:s="library://ns.adobe.com/flex/spark"
	xmlns:resizabletitlewindow="actionScripts.ui.resizableTitleWindow.*"
	skinClass="components.skins.ResizableTitleWindowSkin"
	title="Rename symbol"
	width="600" height="150" minWidth="300" minHeight="150" creationComplete="onRenameViewCreationComplete(event)">

	<fx:Script>
		<![CDATA[
		import elements.skins.DarkButtonSkin;

		import mx.controls.Alert;
		import mx.events.CloseEvent;
		import mx.events.FlexEvent;

		public var newName:String;
		
		private var _oldNameChanged:Boolean = false;

		private var _oldName:String = null;
	
		[Bindable("oldNameChange")]
		public function get oldName():String
		{
			return this._oldName;
		}
	
		public function set oldName(value:String):void
		{
			if(this._oldName === value)
			{
				return;
			}
			this._oldName = value;
			this.newName = value;
			this._oldNameChanged = true;
			this.dispatchEvent(new Event("oldNameChange"));
		}

		override protected function commitProperties():void
		{
			if(this._oldNameChanged)
			{
				this._oldNameChanged = false;
				this.nameInput.text = this._oldName;
				this.nameInput.validateNow();
				this.nameInput.setFocus();
				this.nameInput.selectAll();
			}
			super.commitProperties();
		}
	
		private function saveName():void
		{
			if(!this.newName)
			{
				return;
			}
			var detail:int = Alert.CANCEL;
			if(this.newName !== this._oldName)
			{
				detail = Alert.OK;
			}
			this._oldName = null;
			this.dispatchEvent(new CloseEvent(CloseEvent.CLOSE, false, false, detail));
		}
	
		private function renameButton_onClick(event:MouseEvent):void
		{
			this.saveName();
		}

		private function cancelButton_onClick(event:MouseEvent):void
		{
			this.dispatchEvent(new CloseEvent(CloseEvent.CLOSE));
		}

		private function nameInput_onChange(event:Event):void
		{
			this.newName = this.nameInput.text;
		}

		private function nameInput_onEnter(event:Event):void
		{
			this.saveName();
		}

		private function onRenameViewCreationComplete(event:FlexEvent):void
		{
			cancelButton.setStyle("skinClass", DarkButtonSkin);
		}
		
		]]>
	</fx:Script>

	<s:VGroup width="100%" height="100%"
			  paddingLeft="12" paddingBottom="3" paddingTop="9" paddingRight="13">
		<s:Label text="Rename symbol '{oldName}' and its usages to:"/>
		<s:TextInput id="nameInput" width="100%"
			change="nameInput_onChange(event)"
			enter="nameInput_onEnter(event)"/>
	</s:VGroup>
	
	<resizabletitlewindow:controlBarContent>
		<s:Button label="Rename"
				styleName="darkButton"
				click="renameButton_onClick(event)"/>
			<s:Button id="cancelButton" label="Cancel"
				fontFamily="DejaVuSans" fontSize="12"
				click="cancelButton_onClick(event)"/>
	</resizabletitlewindow:controlBarContent>
</resizabletitlewindow:ResizableTitleWindow>